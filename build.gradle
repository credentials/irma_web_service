apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'eclipse-wtp'

// Property localIP implies localServer (port 8080) and localRelay (port 8081)
if (project.hasProperty("localIP")) {
    ext.localServer = "http://" + localIP + ":8080"
    ext.localRelay =  "http://" + localIP + ":8081"
}

// Set ext.irma_web_service_url if not set. If localServer is set
// then set to localhost, otherwise default remote.
if ( !project.hasProperty("irma_web_service_url") ) {
    if(project.hasProperty("localServer")) {
        ext.irma_web_service_url = localServer + "/irma_web_service"
    } else {
        ext.irma_web_service_url = "https://demo.irmacard.org/tomcat/irma_web_service"
    }
}
System.out.println("Setting localServer to: " + irma_web_service_url);

// Set ext.irma_webr_relay_url if not set. If localServer is set
// then set to localhost, otherwise default remote.
if ( !project.hasProperty("irma_web_relay_url") ){
    if(project.hasProperty("localRelay")) {
        ext.irma_web_relay_url = localRelay + "/irma_web_relay"
    } else {
        ext.irma_web_relay_url = "https://demo.irmacard.org/tomcat/irma_web_relay"
    }
}
System.out.println("Setting localRelay to: " + irma_web_relay_url);

// Add H2 to build dependencies so we can access it
buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.h2database:h2:1.3.176"
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url "https://credentials.github.io/repos/maven2/"
    }
    maven {
        url "http://maven.restlet.org"
    }
    mavenCentral()
}

dependencies {
    providedCompile 'org.apache.tomcat:tomcat-catalina:7.0.41'
    gretty 'org.apache.tomcat:tomcat-catalina:7.0.41'

    compile 'org.apache.tomcat:tomcat-util:7.0.41'

    compile "org.irmacard.idemix:idemix_terminal:0.10.0"

    compile "org.restlet.jee:org.restlet:2.3.2"
    compile "org.restlet.jee:org.restlet.ext.json:2.3.2"
    compile "org.restlet.jee:org.restlet.ext.servlet:2.3.2"
    compile "org.restlet.jee:org.restlet.ext.xml:2.3.2"

    compile 'com.google.code.gson:gson:2.2.2'
    compile 'com.google.zxing:core:2.1.0'
    compile 'com.google.zxing:javase:2.1.0'

    compile 'ch.qos.logback:logback-classic:1.1.2'

    gretty 'commons-dbcp:commons-dbcp:1.4'

    compile 'net.glxn:qrgen:1.4'

    compile 'mysql:mysql-connector-java:5.1.27'

    compile "com.h2database:h2:1.3.176"
}

import org.apache.tools.ant.filters.*

gretty {
    contextConfigFile = file('src/test/resources/jetty-env.xml')
    scanInterval = 10

    webappCopy {
        filesMatching "js/*.js", { FileCopyDetails fileDetails ->
            logger.warn "Filtering: {}", fileDetails.path
            filter ReplaceTokens, tokens: [
                'IRMAWebService': irma_web_service_url,
                'IRMAWebRelay': irma_web_relay_url
            ]
        }
    }
}
